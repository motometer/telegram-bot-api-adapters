buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'checkstyle'
    id "net.researchgate.release" version "2.6.0"
    id "com.jfrog.bintray" version "1.8.4"
}

group = "org.motometer.telegram"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains:annotations:16.0.1"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.motometer.telegram:telegram-bot-api:0.0.6-SNAPSHOT'

    compileOnly "org.immutables:value:2.8.0"
    compileOnly "org.immutables:gson:2.8.0"
    compileOnly "org.projectlombok:lombok:1.18.10"

    annotationProcessor "org.immutables:value:2.8.0"
    annotationProcessor "org.projectlombok:lombok:1.18.10"

    testCompile "org.junit.jupiter:junit-jupiter:5.5.1"
    testCompile "org.assertj:assertj-core:3.13.2"
    testCompile "commons-io:commons-io:2.6"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

release {
    git {
        requireBranch = 'master'
    }
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_PASSWORD')
    publications = ['bintrayPublication']
    pkg {
        repo = 'mvn'
        name = project.name
        userOrg = 'motometer'
        vcsUrl = 'https://github.com/motometer/telegram-bot-core'
        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}