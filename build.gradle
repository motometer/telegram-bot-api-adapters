buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'checkstyle'
    id "net.researchgate.release" version "2.6.0"
    id "com.jfrog.bintray" version "1.8.4"
}

ext {
    testContainversVersion = '1.12.0'
    lombok = "org.projectlombok:lombok:1.18.10"
}

group = "org.motometer.telegram"
sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        compileClasspath += main.output + main.compileClasspath
        compileClasspath += test.output + test.compileClasspath
        runtimeClasspath += main.runtimeClasspath + test.runtimeClasspath
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.6'
    compile 'org.motometer.telegram:telegram-bot-api:0.0.6'
    implementation "org.jetbrains:annotations:16.0.1"

    compileOnly "org.immutables:value:2.8.0"
    compileOnly "org.immutables:gson:2.8.0"
    compileOnly lombok

    annotationProcessor "org.immutables:value:2.8.0"
    annotationProcessor lombok

    integrationTestAnnotationProcessor lombok

    testImplementation "org.junit.jupiter:junit-jupiter:5.5.1"
    testImplementation "org.assertj:assertj-core:3.13.2"

    integrationTestImplementation "commons-io:commons-io:2.6"
    integrationTestImplementation "org.testcontainers:testcontainers:$testContainversVersion"
    integrationTestImplementation "org.testcontainers:junit-jupiter:$testContainversVersion"
    integrationTestImplementation 'com.github.tomakehurst:wiremock-jre8:2.25.1'
    integrationTestImplementation 'org.skyscreamer:jsonassert:1.5.0'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

release {
    git {
        requireBranch = 'master'
    }
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_PASSWORD')
    publications = ['bintrayPublication']
    pkg {
        repo = 'mvn'
        name = project.name
        userOrg = 'motometer'
        vcsUrl = 'https://github.com/motometer/telegram-bot-core'
        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

task integrationTest(type: Test) {
    group 'verification'
    description 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest
